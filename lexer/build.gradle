plugins {
    id 'java-library'
    id 'my-library.java-common-conventions'
    id 'jacoco'
    id "maven-publish"
}

apply plugin: 'checkstyle'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':token')
    implementation project(':filegenerator')
    implementation 'junit:junit:4.12'
}

test {
    useJUnit()

    maxHeapSize = '1G'
}

group = 'edu.austral.ingsis'
version = '1.1.4'

test {
    finalizedBy(jacocoTestReport)
}

jacocoTestReport {
    dependsOn(test)
    reports {
        html.destination(file("${buildDir}/jacocoReport"))
    }
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.0
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
tasks.build.dependsOn(check)

checkstyle {
    toolVersion = '6.16.1'
    config = rootProject.resources.text.fromFile('etc/checkstyle/rules.xml')
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/pedroaraoz/printScript")
            credentials {
                username = project.findProperty("gpr.user") ?: "pedroaraoz"
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
